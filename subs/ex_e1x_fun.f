C
C Subroutine to compute the EXP(X)*E1(X) where E1(X) is the exponential
C integral and X > 0.
C
C The fits are from Abrmowitz and Stegun (p231).
C
	FUNCTION EX_E1X_FUN(X,IFAIL)
	IMPLICIT NONE
C
C Created 14-Sep-1995.   Routine was verified against S13AAF using COMP_EX.
C
	REAL*8 X,EX_E1X_FUN
	INTEGER IFAIL
C
	REAL*8 W(6),A(4),B(4),NUM,DENOM
	INTEGER I
C
	DATA W/-0.57721566D0,0.99999193D0,-0.24991055D0,
	1       0.05519968D0,-0.00976004D0,0.00107857D0/
	DATA A/0.2677737343D0,8.6347608925D0,18.0590169730D0,8.5733287401D0/
	DATA B/3.9584969228D0,21.0996530827D0,25.6329561486D0,9.5733223454D0/
C
	IF(X .LE. 0.0D0)THEN
	 IFAIL=IFAIL+1
	ELSE IF(X .LE. 1)THEN
C
C Error in E1(x)+ln(x) < 2.0E-07
C
	  EX_E1X_FUN=W(6)
	  DO I=5,1,-1
	    EX_E1X_FUN=EX_E1X_FUN*X+W(I)
	  END DO
	  EX_E1X_FUN=EXP(X)*(EX_E1X_FUN-LOG(X))
	ELSE
C
C Error in X.EXP(X). E1(x) < 2.0D-08
C
	  NUM=1.0D0
	  DENOM=1.0D0
	  DO I=4,1,-1
	    NUM=NUM*X+A(I)
	    DENOM=DENOM*X+B(I)
	  END DO
	  EX_E1X_FUN=NUM/DENOM/X
	END IF
C
	RETURN
	END
