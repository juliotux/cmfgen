	SUBROUTINE SET_DC_OR_POP_OR_TX(YV,LEV,HE2,HE2LTE,EDGEHE2,NHE2,ND,T,X,DESC,FLAG)
	IMPLICIT NONE
!
! Aletered 20-Mar-1997 : YV is now REAL*8
!
	INTEGER NHE2,ND,LEV,I,J
	REAL*8 YV(ND)
	REAL*8 T(ND)
	REAL*8 EDGEHE2(NHE2)
	REAL*8 HE2(NHE2,ND),HE2LTE(NHE2,ND)
	CHARACTER*(*) X,DESC
	CHARACTER*20 XSPEC
	LOGICAL FLAG,LOCAL
	INTEGER, PARAMETER :: T_OUT=6
!
	REAL*8 T1
	REAL*8 DELTA_T
	REAL*8 T_EXCITE
!
        COMMON/CONSTANTS/ CHIBF,CHIFF,HDKT,TWOHCSQ
        REAL*8 CHIBF,CHIFF,HDKT,TWOHCSQ
!
	LOCAL=.FALSE.
	I=LEN_TRIM(X)
	J=INDEX(X,'_')
	IF(J .EQ. 0)THEN
	    WRITE(T_OUT,*)'Error in SET_DC_OR_POP - missing _ from option'
	    WRITE(T_OUT,*)'NSPEC=',X
	    RETURN
	ELSE
	  XSPEC=X(J+1:)
	END IF
!	
	IF(X(1:2) .EQ. 'DC' .AND. XSPEC .EQ. DESC)THEN
	  IF(LEV .LE. 0 .OR. LEV .GT. NHE2)THEN
	    WRITE(T_OUT,*)'Error in SET_DC_OR_POP_OR_TX - invalid level'
	    WRITE(T_OUT,*)'Lev=',LEV
	    WRITE(T_OUT,*)'NSPEC=',NHE2
	    RETURN
	  END IF
	  DO J=1,ND
	    YV(J)=LOG10(HE2(LEV,J)/HE2LTE(LEV,J))
	  END DO
	  LOCAL=.TRUE.
	ELSE IF(X(1:2) .EQ. 'TX' .AND. XSPEC .EQ. DESC)THEN
	  IF(LEV .LE. 0 .OR. LEV .GT. NHE2)THEN
	    WRITE(T_OUT,*)'Error in SET_DC_OR_POP_OR_TX - invalid level'
	    WRITE(T_OUT,*)'Lev=',LEV
	    WRITE(T_OUT,*)'NSPEC=',NHE2
	    RETURN
	  END IF
	  T_EXCITE=T(ND)
	  I=LEV
	  DO J=ND,1,-1
	    DELTA_T=100
	    DO WHILE(DELTA_T .GT. 1.0E-08)
	      T1=(HE2(I,J)/HE2LTE(I,J))*EXP(HDKT*EDGEHE2(I)*(1.0D0/T(J)-1.0D0/T_EXCITE))*
	1           (T_EXCITE/T(J))**1.5D0
	      DELTA_T=(T1-1.0D0)*T_EXCITE/T1/(1.5D0+HDKT*EDGEHE2(I)/T_EXCITE)
	      T_EXCITE=T_EXCITE-DELTA_T
	    END DO
	    YV(J)=T_EXCITE
	  END DO
	  LOCAL=.TRUE.
	ELSE IF(X(1:3) .EQ. 'RAT' .AND. LEV .GT. NHE2
	1      .AND. XSPEC .EQ. DESC)THEN
	  DO J=1,ND
	    T1=0.0		!By using T1 advoids log(ZERO).
	    DO I=1,NHE2
	      T1=T1+HE2(I,J)
	    END DO
	    YV(J)=LOG10(T1)
	  END DO
	  LOCAL=.TRUE.
	ELSE IF(XSPEC .EQ. DESC )THEN  		!RAT or POP
	  IF(LEV .LE. 0 .OR. LEV .GT. NHE2)THEN
	    WRITE(T_OUT,*)'Error in SET_DC_OR_POP_OR_TX - invalid level'
	    WRITE(T_OUT,*)'Lev=',LEV
	    WRITE(T_OUT,*)'NSPEC=',NHE2
	    RETURN
	  END IF
	  DO J=1,ND
	    YV(J)=LOG10(HE2(LEV,J))
	  END DO
	  LOCAL=.TRUE.
	END IF
!
	IF(LOCAL .AND. FLAG)THEN
	  WRITE(T_OUT,*)'Error - POP, DC or RAT has been called twice'
	ELSE IF(LOCAL)THEN
	  FLAG=LOCAL
	END IF
!
	RETURN
	END
