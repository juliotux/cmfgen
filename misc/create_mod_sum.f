!
! Simple program to read in a FILE (MOD_DIRS) containg a list of MODEL directories.
! Program reads the MOD_SUM file in each of these directories, and outputs a summary
! file (MODEL_SUMMARY).
!
	PROGRAM CREATE_MOD_SUM
	USE GEN_IN_INTERFACE
	IMPLICIT NONE
!
	REAL*8 T1,T2,T3
	INTEGER K,IOS
	INTEGER LDIR
	INTEGER I,J
!
	LOGICAL NO_HYD_RD
	LOGICAL FILE_PRES
!	
	CHARACTER(LEN=200) STRING
	CHARACTER(LEN=100) FRM
	CHARACTER(LEN=100) FILENAME,MOD_FILENAME
	CHARACTER(LEN=10)  LSTAR,MDOT
	CHARACTER(LEN=9)  RSTAR,VINF
	CHARACTER(LEN=8)  TEFF
	CHARACTER(LEN=6)  LOGG
	CHARACTER(LEN=8)  NHeH,XCARB,XNIT,XOXY,XNEON
	CHARACTER(LEN=6)  ND
	CHARACTER(LEN=6)  NT
	CHARACTER(LEN=8)  BETA
	CHARACTER(LEN=6)  CL_F
	CHARACTER(LEN=7)  CL_VF
	CHARACTER(LEN=11) DATE
!
! Initialize header variables. Done this way as change in string length does
! not require a change here.
!
	LSTAR='L/Lsun'; MDOT='Mdot'; VINF='Vinf'
	RSTAR='R/Rsun'; LOGG='Logg'; NT='NT'; ND='ND'
	CL_F='f'; CL_VF='V(f)';  BETA='Beta'
	XCARB='X(C/s)'; XNIT='X(N/s)'; XOXY='X(O/s)'; XNEON='X(Ne/s)'
	NHeH='N(He/H)'; TEFF='Teff'; DATE='Date'
	FILENAME=' '
!
	NO_HYD_RD=.FALSE.
	CALL GEN_IN(NO_HYD_RD,'Are the models devoid of hydrogen')
	IF(NO_HYD_RD)THEN
	  NHeH=' X(He)'
	  XCARB='  X(C)'; XNIT='  X(N)'; XOXY='  X(O)'; XNEON='  X(Ne)'
	END IF	
!
	TEFF=ADJUSTR(TEFF)
	LOGG=ADJUSTR(LOGG)
	LSTAR=ADJUSTR(LSTAR)
	RSTAR=ADJUSTR(RSTAR)
	MDOT=ADJUSTR(MDOT)
	CL_F=ADJUSTR(CL_F)
	CL_VF=ADJUSTR(CL_VF)
	VINF=ADJUSTR(VINF)
	BETA=ADJUSTR(BETA)
	NHeH=ADJUSTR(NHeH)
	XCARB=ADJUSTR(XCARB)
	XNIT=ADJUSTR(XNIT)
	XOXY=ADJUSTR(XOXY)
	XNEON=ADJUSTR(XNEON)
	NT=ADJUSTR(NT)
	ND=ADJUSTR(ND)
	DATE=ADJUSTR(DATE)
!
	OPEN(UNIT=12,FILE='MODEL_SUMMARY',STATUS='UNKNOWN',ACTION='WRITE')
!
! Outut header.
! Note: This write should be identical to that at the end of the program.
!
	OPEN(UNIT=8,FILE='MOD_DIRS',STATUS='OLD',ACTION='READ')
	LDIR=1
	DO WHILE(1 .EQ. 1)
	  READ(8,'(A)',END=500)FILENAME
	  LDIR=MAX(LDIR,LEN_TRIM(FILENAME))
	END DO
500	CONTINUE
	LDIR=LDIR+1
!
	WRITE(FRM,'(I3)')LDIR
	FRM=ADJUSTL(FRM)
	FILENAME=' '
	IF(NO_HYD_RD)THEN
	  FRM='(A,T'//TRIM(FRM)//',15A,2X,A11)'
	  WRITE(6,*)LDIR,FRM
	  WRITE(12,FRM)TRIM(ADJUSTL(FILENAME)),TEFF,LOGG,LSTAR,RSTAR,MDOT,
	1                          CL_F,CL_VF,VINF,BETA,NHeH,XCARB,XOXY,XNEON,ND,NT,DATE
	ELSE
	  FRM='(A,T'//TRIM(FRM)//',15A,2X,A11)'
	  WRITE(12,FRM)TRIM(ADJUSTL(FILENAME)),TEFF,LOGG,LSTAR,RSTAR,MDOT,
	1                          CL_F,CL_VF,VINF,BETA,NHeH,XCARB,XNIT,XOXY,ND,NT,DATE
	END IF
!
	REWIND(UNIT=8)	
	DO WHILE(1 .EQ. 1)
	  READ(8,'(A)',END=9999)FILENAME
!
	  MOD_FILENAME=TRIM(FILENAME)//'/MOD_SUM'
	  INQUIRE(FILE=MOD_FILENAME,EXIST=FILE_PRES)
	  IF(.NOT. FILE_PRES)THEN
	    WRITE(12,'(A)')TRIM(FILENAME)
	    GOTO 5000
	  END IF
!
	  OPEN(UNIT=10,FILE=MOD_FILENAME,STATUS='OLD',ACTION='READ')
!
	  STRING=' '
	  DO WHILE( INDEX(STRING,'Finalized') .EQ. 0 )
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'on:')+3
	  STRING=ADJUSTL(STRING(K:))
	  DATE=STRING(1:11)
!
	  DO WHILE( INDEX(STRING,'ND[') .EQ. 0 )
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'ND[')
	  STRING=STRING(K+3:)
	  K=INDEX(STRING,']')
	  ND=STRING(1:K-1)
!
	  K=INDEX(STRING,'NT[')
	  STRING=STRING(K+3:)
	  K=INDEX(STRING,']')
	  NT=STRING(1:K-1)
	  WRITE(6,*)'NT=',TRIM(NT)
!
	  DO WHILE( INDEX(STRING,'L*') .EQ. 0 )
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,' ')
	  READ(STRING(4:K),*)T1
	  WRITE(LSTAR,'(ES8.2)')T1
	  WRITE(6,*)'Done LSTAR=',TRIM(LSTAR)
!
	  STRING=STRING(K:)
	  K=INDEX(STRING,'Mdot')
	  STRING=STRING(K:)
	  READ(STRING(6:14),*)T1
	  WRITE(MDOT,'(ES8.2)')T1
!
	  DO WHILE( INDEX(STRING,'6.667E-01') .EQ. 0 )
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'Rsun')
	  STRING=STRING(K+5:)
	  K=INDEX(STRING,' ')
	  RSTAR=STRING(1:K)
	  READ(RSTAR,*)T1
	  IF(T1 .LT. 1.0 .AND. T1 .GT. 0.1)THEN
	    WRITE(RSTAR,'(F5.3)')T1
	  END IF
!
	  K=INDEX(STRING,'Teff')
	  STRING=STRING(K+8:)
	  READ(STRING,*)T1
	  WRITE(TEFF,'(F5.2)')T1/1.0D+04
!
	  K=INDEX(STRING,'Log g')
	  IF(K .EQ. 0)THEN
	    LOGG=' '
	  ELSE
	    STRING=STRING(K+6:)
	    LOGG=STRING(1:4)
	  END IF
!
	  DO WHILE( INDEX(STRING,'Vinf1') .EQ. 0)
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'Vinf1')
	  STRING=STRING(K+6:)
	  K=INDEX(STRING,' ')
	  VINF=STRING(1:K)
	  K=LEN_TRIM(VINF)
	  DO WHILE(VINF(K:K) .EQ. '0' .AND. VINF(K-1:K-1) .NE. '.')
	    VINF(K:K)=' '
	    K=K-1
	  END DO
!
	  DO WHILE( INDEX(STRING,'Beta1') .EQ. 0)
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'Beta1')
	  STRING=STRING(K+6:)
	  K=INDEX(STRING,' ')
	  READ(STRING(1:K),*)T1
	  WRITE(BETA,'(F3.1)')T1
!
	  DO WHILE( INDEX(STRING,'HYD') .EQ. 0)
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'HYD')+4
	  READ(STRING(K:),*)T1
	  IF(T1 .NE. 0.0D0 .AND. NO_HYD_RD)THEN
	  END IF
!
	  READ(10,'(A)')STRING
	  K=INDEX(STRING,'HE')+4
	  IF(NO_HYD_RD)THEN
	    READ(STRING(K:),*)T2,T3
	    WRITE(NHeH,'(F6.3)')T3
	  ELSE
	    READ(STRING(K:),*)T2
	    WRITE(NHeH,'(F5.2)')T2
	  END IF
!
	  READ(10,'(A)')STRING
	  K=INDEX(STRING,'CARB')+4
	  READ(STRING(K:),*)T3,T2,T1
	  IF(NO_HYD_RD)THEN
	    WRITE(XCARB,'(F5.3)')T2
	  ELSE
	    WRITE(XCARB,'(F5.3)')T1
	  END IF
!	
	  READ(10,'(A)')STRING
	  K=INDEX(STRING,'NIT')+4
	  READ(STRING(K:),*)T3,T2,T1
	  IF(NO_HYD_RD)THEN
	    WRITE(XNIT,'(F6.3)')T2
	  ELSE
	    WRITE(XNIT,'(F6.3)')T1
	  END IF
!	
	  READ(10,'(A)')STRING
	  K=INDEX(STRING,'OXY')+4
	  READ(STRING(K:),*)T3,T2,T1
	  IF(NO_HYD_RD)THEN
	    WRITE(XOXY,'(F5.3)')T2
	    IF(T3 .LT. 0.05)WRITE(XOXY,'(F6.4)')T2
	  ELSE 
	    WRITE(XOXY,'(F5.3)')T1
	  END IF
!
	  READ(10,'(A)')STRING        !Fluorine
	  IF(INDEX(STRING,'FLU') .NE. 0)READ(10,'(A)')STRING
	  K=INDEX(STRING,'NEON')+5
	  READ(STRING(K:),*)T3,T2,T1
	  IF(NO_HYD_RD)THEN
	    WRITE(XNEON,'(F6.3)')T2
	    IF(T2 .LT. 0.05)WRITE(XNEON,'(F6.4)')T2
	  ELSE
	    WRITE(XNEON,'(F6.3)')T1
	  END IF
!
	  IOS=0
	  DO WHILE(INDEX(STRING,'CL_P') .EQ. 0 .AND. IOS .EQ. 0)
	    READ(10,'(A)',IOSTAT=IOS)STRING
	  END DO
	  IF(IOS .EQ. 0)THEN
	    K=INDEX(STRING,'CL_P_1')+7
	    READ(STRING(K:),*)T1
	    WRITE(CL_F,'(F4.2)')T1
	    K=INDEX(STRING,'CL_P_2')+7
	    READ(STRING(K:),*)T1
	    WRITE(CL_VF,'(F6.1)')T1
	  ELSE
	    WRITE(CL_F,'(F4.2)')1.0D0
	    WRITE(CL_VF,'(F6.1)')0.0D0
	  END IF
!	
	  CLOSE(UNIT=10)
	  WRITE(6,*)'Read ',TRIM(FILENAME)
!
	  MOD_FILENAME=TRIM(FILENAME)//'/HYDRO_DEFAULTS'
	  INQUIRE(FILE=MOD_FILENAME,EXIST=FILE_PRES)
	  IF(FILE_PRES)THEN
	  OPEN(UNIT=10,FILE=MOD_FILENAME,STATUS='OLD')
	    DO WHILE(1 .EQ. 1)
	      READ(10,'(A)',END=2000)STRING
	      IF(INDEX(STRING,'BETA2') .NE. 0)THEN
	         STRING=ADJUSTL(STRING)
	         I=INDEX(STRING,' ')
	         BETA=ADJUSTL(BETA)
	         BETA=TRIM(BETA)//'/'//STRING(1:I)
	         CLOSE(UNIT=10)
	         EXIT
	       END IF   
            END DO
	  END IF
2000      CONTINUE
	   
	  TEFF=ADJUSTR(TEFF)
	  LOGG=ADJUSTR(LOGG)
	  LSTAR=ADJUSTR(LSTAR)
	  RSTAR=ADJUSTR(RSTAR)
	  MDOT=ADJUSTR(MDOT)
	  CL_F=ADJUSTR(CL_F)
	  CL_VF=ADJUSTR(CL_VF)
	  VINF=ADJUSTR(VINF)
	  BETA=ADJUSTR(BETA)
	  NHeH=ADJUSTR(NHeH)
	  XCARB=ADJUSTR(XCARB)
	  XNIT=ADJUSTR(XNIT)
	  XOXY=ADJUSTR(XOXY)
	  XNEON=ADJUSTR(XNEON)
	  NT=ADJUSTR(NT)
	  ND=ADJUSTR(ND)
!
	  WRITE(FRM,'(I5)')LDIR
	  FRM=ADJUSTL(FRM)
	  IF(NO_HYD_RD)THEN
	    FRM='(A,T'//TRIM(FRM)//',15A,2X,A11)'
	    WRITE(12,FRM)TRIM(ADJUSTL(FILENAME)),TEFF,LOGG,LSTAR,RSTAR,MDOT,
	1                          CL_F,CL_VF,VINF,BETA,NHeH,XCARB,XOXY,XNEON,ND,NT,DATE
	  ELSE
	    FRM='(A,T'//TRIM(FRM)//',15A,2X,A11)'
	    WRITE(12,FRM)TRIM(ADJUSTL(FILENAME)),TEFF,LOGG,LSTAR,RSTAR,MDOT,
	1                          CL_F,CL_VF,VINF,BETA,NHeH,XCARB,XNIT,XOXY,ND,NT,DATE
	  END IF
	  FLUSH(UNIT=12)
5000	CONTINUE
!
	END DO
9999	CONTINUE
!
	STOP
	END
